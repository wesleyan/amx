PROGRAM_NAME='DVD1'
(***********************************************************)
(*               CONSTANT DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_CONSTANT

(* !!                                 !! *)
(* !! DO NOT OVERRIDE THESE CONSTANTS !! *)
(* !!                                 !! *)

#IF_NOT_DEFINED SYSCALL_MISC
#DEFINE SYSCALL_MISC
NO_BUTTON   = 0
NO_FUNCTION = 256
#END_IF

#IF_NOT_DEFINED XPORT_CONST
#DEFINE XPORT_CONST
PLAY    = 1
STOP    = 2
PAUSE   = 3
FFWD    = 4
REW     = 5
SFWD    = 6
SREV    = 7
REC     = 8
#END_IF

#IF_NOT_DEFINED FEEDBACK_CONST
#DEFINE FEEDBACK_CONST
PLAY_FB    = 241
STOP_FB    = 242
PAUSE_FB   = 243
FFWD_FB    = 244
REW_FB     = 245
SFWD_FB    = 246
SREV_FB    = 247
REC_FB     = 248
#END_IF

(* cd will go into stop after being paused for a certain time *)
#IF_NOT_DEFINED DVD1_PAUSE_TO_STOP
DVD1_PAUSE_TO_STOP = 6000 (* 10 min *)
#END_IF

(* button feedback flag *)
#IF_NOT_DEFINED DVD1_DEFEAT_FEEDBACK
DVD1_DEFEAT_FEEDBACK = 0
#END_IF

(* pulse delay *)
#IF_NOT_DEFINED DVD1_PULSE_DELAY
DVD1_PULSE_DELAY = 7
#END_IF

(***********************************************************)
(*             SUBROUTINE DEFINITIONS GO BELOW             *)
(***********************************************************)

(* controls a deck with discrete searches                    *)
(* play to get out of pause                                  *)
(* use 0 as a button channel if the button doesn't exist     *)
(* use 256 as a button channel if the function should not be *)
(*   available to the user                                   *)

#IF_NOT_DEFINED __NETLINX__
DEFINE_CALL 'DVD1' (DECK,PANEL,PLAYB,STOPB,PAUSEB,FFWDB,REWB,SFWDB,
  SREVB,FIRST)
#END_IF
#IF_DEFINED __NETLINX__
DEFINE_CALL 'DVD1' (DEV DECK,DEV PANEL,PLAYB,STOPB,PAUSEB,FFWDB,REWB,SFWDB,
  SREVB,FIRST)
#END_IF
(* DECK   - device number of cd control                         *)
(* PANEL  - control panel device number                         *)
(* PLAYB  - channel number of play button                       *)
(* PAUSEB - channel number of pause button                      *)
(* STOPB  - channel number of stop button                       *)
(* FWDB   - channel number of ffwd button                       *)
(* REWB   - channel number of rewind button                     *)
(* SFWDB  - channel number of search fwd button                 *)
(* SREVB  - channel number of search rev button                 *)
(* FIRST  - offset for channels:                                *)
(*          low byte=desired channel for play                   *)
(*          high byte=desired channel for play feedback         *)
(*          ex: PLAY located at channel 15, and feedback wanted *)
(*            to start at channel 210, FIRST=(210*256)+15       *)
(*          if FIRST=0, channels are at default (PLAY=1,FB=241) *)
LOCAL_VAR
    OFFSET_FN
    OFFSET_FB
    LAST_FN
{
#IF_DEFINED SYSCALL_NOTIFY
    PUSH[PANEL,PLAYB]
    PUSH[PANEL,STOPB]
    PUSH[PANEL,PAUSEB]
    PUSH[PANEL,FFWDB]
    PUSH[PANEL,REWB]
    PUSH[PANEL,SFWDB]
    PUSH[PANEL,SREVB]
    {
        SEND_STRING 0,"'IN SYSCALL ',39,'DVD1',39,' [',__SYSTEM_CALL_NUM__,
          ']',13,10"
    }
#END_IF

    (* get offsets if any *)
    IF (FIRST BAND $00FF)
        OFFSET_FN=(FIRST BAND $00FF)-PLAY
    ELSE
        OFFSET_FN=0
    IF (FIRST BAND $FF00)
        OFFSET_FB=((FIRST BAND $FF00)/$FF)-PLAY_FB
    ELSE
        OFFSET_FB=0

    PUSH[PANEL,PLAYB]
    {
        CANCEL_WAIT 'DVD1 PAUSE TO STOP'
        CANCEL_WAIT 'DVD1 PUT INTO STOP'
        SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
        MIN_TO [DECK,OFFSET_FN+PLAY]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
        TO [DECK,OFFSET_FN+PLAY]
#END_IF
        SYSTEM_CALL 'FEEDBACK' (DECK,PLAY,FIRST)
    }

    PUSH[PANEL,STOPB]
    {
        CANCEL_WAIT 'DVD1 PAUSE TO STOP'
        CANCEL_WAIT 'DVD1 PUT INTO STOP'
        SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
        PULSE [DECK,OFFSET_FN+STOP]
        WAIT DVD1_PULSE_DELAY 'DVD1 PUT INTO STOP'
          PULSE [DECK,OFFSET_FN+STOP]
        SYSTEM_CALL 'FEEDBACK' (DECK,STOP,FIRST)
    }

    PUSH[PANEL,PAUSEB]
        SELECT
        {
            ACTIVE ([DECK,OFFSET_FB+PAUSE_FB]
              AND PLAYB<NO_FUNCTION):
            {
                CANCEL_WAIT 'DVD1 PAUSE TO STOP'
                CANCEL_WAIT 'DVD1 PUT INTO STOP'
                SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
                MIN_TO [DECK,OFFSET_FN+PLAY]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
                TO [DECK,OFFSET_FN+PLAY]
#END_IF
                SYSTEM_CALL 'FEEDBACK' (DECK,PLAY,FIRST)
            }
            ACTIVE ([DECK,OFFSET_FB+PLAY_FB]):
            {
                CANCEL_WAIT 'DVD1 PAUSE TO STOP'
                CANCEL_WAIT 'DVD1 PUT INTO STOP'
                SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
                MIN_TO [DECK,OFFSET_FN+PAUSE]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
                TO [DECK,OFFSET_FN+PAUSE]
#END_IF
                SYSTEM_CALL 'FEEDBACK' (DECK,PAUSE,FIRST)
                IF (DVD1_PAUSE_TO_STOP)
                    WAIT DVD1_PAUSE_TO_STOP 'DVD1 PAUSE TO STOP'
                        SYSTEM_CALL 'FUNCTION' (DECK,STOP,FIRST)
            }
        }

    PUSH[PANEL,FFWDB]
        SELECT
        {
            ACTIVE (SFWDB=NO_BUTTON
             AND ([DECK,OFFSET_FB+PLAY_FB]
              OR [DECK,OFFSET_FB+SFWD_FB]
              OR [DECK,OFFSET_FB+SREV_FB])):
            {
                CANCEL_WAIT 'DVD1 PAUSE TO STOP'
                CANCEL_WAIT 'DVD1 PUT INTO STOP'
                SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
                MIN_TO [DECK,OFFSET_FN+SFWD]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
                TO [DECK,OFFSET_FN+SFWD]
#END_IF
                LAST_FN = 0 (* SO RELEASE STAYS HERE *)
                SYSTEM_CALL 'FEEDBACK' (DECK,SFWD,FIRST)
            }
            ACTIVE ([DECK,OFFSET_FB+PAUSE_FB]
                OR [DECK,OFFSET_FB+PLAY_FB]):
            {
                CANCEL_WAIT 'DVD1 PAUSE TO STOP'
                CANCEL_WAIT 'DVD1 PUT INTO STOP'
                SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
                MIN_TO [DECK,OFFSET_FN+FFWD]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
                TO [DECK,OFFSET_FN+FFWD]
#END_IF
                SELECT
                {
                  ACTIVE ([DECK,OFFSET_FB+PAUSE_FB]): { LAST_FN = PAUSE }
                  ACTIVE ([DECK,OFFSET_FB+PLAY_FB]):  { LAST_FN = PLAY  }
                  ACTIVE (1):                         { LAST_FN = 0   }
                }
                SYSTEM_CALL 'FEEDBACK' (DECK,FFWD,FIRST)
            }
        }

    PUSH[PANEL,SFWDB]
        IF ([DECK,OFFSET_FB+PLAY_FB]
         OR [DECK,OFFSET_FB+SFWD_FB]
         OR [DECK,OFFSET_FB+SREV_FB])
        {
            CANCEL_WAIT 'DVD1 PAUSE TO STOP'
            CANCEL_WAIT 'DVD1 PUT INTO STOP'
            SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
            MIN_TO [DECK,OFFSET_FN+SFWD]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
            TO [DECK,OFFSET_FN+SFWD]
#END_IF
            SYSTEM_CALL 'FEEDBACK' (DECK,SFWD,FIRST)
        }

    PUSH[PANEL,REWB]
        SELECT
        {
            ACTIVE (SREVB=NO_BUTTON
             AND ([DECK,OFFSET_FB+PLAY_FB]
              OR [DECK,OFFSET_FB+SFWD_FB]
              OR [DECK,OFFSET_FB+SREV_FB])):
            {
                CANCEL_WAIT 'DVD1 PAUSE TO STOP'
                CANCEL_WAIT 'DVD1 PUT INTO STOP'
                SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
                MIN_TO [DECK,OFFSET_FN+SREV]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
                TO [DECK,OFFSET_FN+SREV]
#END_IF
                LAST_FN = 0 (* SO RELEASE STAYS HERE *)
                SYSTEM_CALL 'FEEDBACK' (DECK,SREV,FIRST)
            }
            ACTIVE ([DECK,OFFSET_FB+PAUSE_FB]
                 OR [DECK,OFFSET_FB+PLAY_FB]):
            {
                CANCEL_WAIT 'DVD1 PAUSE TO STOP'
                CANCEL_WAIT 'DVD1 PUT INTO STOP'
                SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
                MIN_TO [DECK,OFFSET_FN+REW]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
                TO [DECK,OFFSET_FN+REW]
#END_IF
                SELECT
                {
                  ACTIVE ([DECK,OFFSET_FB+PAUSE_FB]): { LAST_FN = PAUSE }
                  ACTIVE ([DECK,OFFSET_FB+PLAY_FB]):  { LAST_FN = PLAY  }
                  ACTIVE (1):                         { LAST_FN = 0   }
                }
                SYSTEM_CALL 'FEEDBACK' (DECK,REW,FIRST)
            }
        }

    PUSH[PANEL,SREVB]
        IF ([DECK,OFFSET_FB+PLAY_FB]
         OR [DECK,OFFSET_FB+SFWD_FB]
         OR [DECK,OFFSET_FB+SREV_FB])
        {
            CANCEL_WAIT 'DVD1 PAUSE TO STOP'
            CANCEL_WAIT 'DVD1 PUT INTO STOP'
            SYSTEM_CALL 'ALL OFF' (DECK,FIRST)
#IF_NOT_DEFINED MASTER_BELOW_V330
            MIN_TO [DECK,OFFSET_FN+SREV]
#END_IF
#IF_DEFINED MASTER_BELOW_V330
            TO [DECK,OFFSET_FN+SREV]
#END_IF
            SYSTEM_CALL 'FEEDBACK' (DECK,SREV,FIRST)
        }

    RELEASE[PANEL,FFWDB]
    RELEASE[PANEL,REWB]
    {
        IF (LAST_FN
          AND ([DECK,OFFSET_FB+FFWD_FB]
            OR [DECK,OFFSET_FB+SFWD_FB]
            OR [DECK,OFFSET_FB+REW_FB]
            OR [DECK,OFFSET_FB+SREV_FB]))
            SYSTEM_CALL 'FEEDBACK' (DECK,LAST_FN,FIRST)
        LAST_FN=0
    }


    IF (!DVD1_DEFEAT_FEEDBACK)
    {
        [PANEL,PLAYB]  = [DECK,OFFSET_FB+PLAY_FB]
        [PANEL,STOPB]  = [DECK,OFFSET_FB+STOP_FB]
        [PANEL,PAUSEB] = [DECK,OFFSET_FB+PAUSE_FB]
        [PANEL,FFWDB]  = [DECK,OFFSET_FB+FFWD_FB]
                      OR (SFWDB=NO_BUTTON AND [DECK,OFFSET_FB+SFWD_FB])
        [PANEL,REWB]   = [DECK,OFFSET_FB+REW_FB]
                      OR (SREVB=NO_BUTTON AND [DECK,OFFSET_FB+SREV_FB])
        [PANEL,SFWDB]  = [DECK,OFFSET_FB+SFWD_FB]
        [PANEL,SREVB]  = [DECK,OFFSET_FB+SREV_FB]
    }
}

(***********************************************************)
(*                     END OF PROGRAM                      *)
(*        DO NOT PUT ANY CODE BELOW THIS COMMENT           *)
(***********************************************************)
