PROGRAM_NAME='VOL1'
(***********************************************************)
(*               CONSTANT DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_CONSTANT

(* !!                                 !! *)
(* !! DO NOT OVERRIDE THESE CONSTANTS !! *)
(* !!                                 !! *)

(* button feedback flag *)
#IF_NOT_DEFINED VOL1_DEFEAT_FEEDBACK
VOL1_DEFEAT_FEEDBACK = 0
#END_IF

(***********************************************************)
(*             SUBROUTINE DEFINITIONS GO BELOW             *)
(***********************************************************)

(* controls standard volume functions: ramp up, ramp dn, mute *)
(* mute is a toggling function, and ramping will cancel mute  *)

#IF_NOT_DEFINED __NETLINX__
DEFINE_CALL 'VOL1' (PANEL,UPB,DOWNB,MUTEB,CARD,UPC,DOWNC,MUTEC)
#END_IF
#IF_DEFINED __NETLINX__
DEFINE_CALL 'VOL1' (DEV PANEL,UPB,DOWNB,MUTEB,DEV CARD,UPC,DOWNC,MUTEC)
#END_IF
(* PANEL - control panel device number                         *)
(* UPB   - channel number of up button                         *)
(* DOWNB - channel number of down button                       *)
(* MUTEB - channel number of mute button                       *)
(* CARD  - volume control device number                        *)
(* UPC   - channel number of up function                       *)
(* DOWNC - channel number of down function                     *)
(* MUTEC - channel number of mute function                     *)
{
#IF_DEFINED SYSCALL_NOTIFY
    PUSH[PANEL,UPB]
    PUSH[PANEL,DOWNB]
    PUSH[PANEL,MUTEB]
        SEND_STRING 0,"'IN SYSCALL ',39,'VOL1',39,' [',__SYSTEM_CALL_NUM__,
          ']',13,10"
#END_IF

    PUSH[PANEL,UPB] (* up *)
        IF (![CARD,DOWNC])
        {
            OFF [CARD,MUTEC]
            ON [CARD,UPC]
        }
    RELEASE[PANEL,UPB]
        IF (![CARD,MUTEC])
            OFF [CARD,UPC]

    PUSH[PANEL,DOWNB] (* down *)
        IF (![CARD,UPC])
        {
            OFF [CARD,MUTEC]
            ON [CARD,DOWNC]
        }
    RELEASE[PANEL,DOWNB]
        IF (![CARD,MUTEC])
            OFF [CARD,DOWNC]

    PUSH[PANEL,MUTEB] (* mute *)
    {
        [CARD,MUTEC] = ![CARD,MUTEC]
        IF ([CARD,MUTEC])
        {
            OFF [CARD,UPC]
            OFF [CARD,DOWNC]
        }
    }


    IF (!VOL1_DEFEAT_FEEDBACK)
    {
        [PANEL,UPB]  =[CARD,UPC]
        [PANEL,DOWNB]=[CARD,DOWNC]
        [PANEL,MUTEB]=[CARD,MUTEC]
    }
}

(***********************************************************)
(*                     END OF PROGRAM                      *)
(*        DO NOT PUT ANY CODE BELOW THIS COMMENT           *)
(***********************************************************)

